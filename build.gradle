/*
 * Copyright 2017 The Hyve
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'de.mobilej.unmock:UnMockPlugin:0.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        classpath 'org.owasp:dependency-check-gradle:4.0.2'
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'idea'
    apply plugin: 'org.owasp.dependencycheck'

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

    version = '1.0.0-SNAPSHOT'
    ext.versionCode = 29
    group = 'org.radarbase'
    ext.githubRepoName = 'RADAR-base/radar-commons-android'

    ext.githubUrl = 'https://github.com/' + githubRepoName + '.git'
    ext.issueUrl = 'https://github.com/' + githubRepoName + '/issues'
    ext.website = 'http://radar-base.org'

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                      //
//---------------------------------------------------------------------------//

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }

    idea {
        module {
            downloadSources = true
        }
    }

    dependencyCheck {
        format = "ALL"
    }
    tasks.matching { it instanceof Test }.all {
        testLogging {
            showExceptions = true
            showStandardStreams = true
            showCauses = true
            showStackTraces = true
            exceptionFormat 'full'
        }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = '1.8'
            apiVersion = '1.3'
            languageVersion = '1.3'
        }
    }
}

wrapper {
    gradleVersion '5.5.1'
}
